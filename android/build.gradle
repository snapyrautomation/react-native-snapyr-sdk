buildscript {
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
            jcenter()
            flatDir{
              dirs 'libs'
            }
        }

        dependencies {
            classpath 'com.android.tools.build:gradle:3.5.3'
        }
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('SnapyrRnSdk_compileSdkVersion', 29)
    defaultConfig {
        minSdkVersion safeExtGet('SnapyrRnSdk_minSdkVersion', 16)
        targetSdkVersion safeExtGet('SnapyrRnSdk_targetSdkVersion', 29)
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    mavenCentral()
    jcenter()
    flatDir{
      dirs 'libs'
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"  // From node_modules
    implementation 'androidx.lifecycle:lifecycle-process:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation platform('com.google.firebase:firebase-bom:27.1.0')
    implementation 'com.google.firebase:firebase-messaging'
    if (!safeExtGet('SnapyrRnSdk_omitSnapyrAndroidSDK', false)) {
        implementation 'com.snapyr.sdk.android:snapyr:1.2.2-beta3'
    } else {
        logger.warn("===========================================================")
        logger.warn("Snapyr RN: OMITTING ANDROID SDK! Consuming project must include Snapyr")
        logger.warn("===========================================================")
    }
    // implementation fileTree(dir: "libs", include: ["*.aar"])
}
